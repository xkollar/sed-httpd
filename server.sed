#!/usr/bin/sed -nf
#
# Sed server
# ----------
#
# Author: Matej Kollar <208115@mail.muni.cz>
#
# Sokoban example taken from sokoban.sed by aurélio marinho jargas
#
#     * http://aurelio.net/projects/sedsokoban/
#     * https://github.com/aureliojargas/sokoban.sed
#     * http://sed.sourceforge.net/grabbag/scripts/sokoban.sed
#
# Number incrementation taken from GNU sed documentation
#
#     * https://www.gnu.org/software/sed/manual/sed.html#Examples
#
/^$/d
H
/^\r$/{
    x
        # Remove leading newline introduced by adding to empty hold space
        s/^\n//
        # Escape slashes and pipes
        s/[\|]/\\&/g
        # Replace linebreaks
        s/\r\n\|\r\|\n/|/g
        # Wrap into request "data structure"
        s/^.*$/|Request:&/
    x

    # Build basic response headers
    x
        s/$/|ResponseHeaders:/
        s/$/HTTP\/1.0 200 OK\r\n/
        s/$/Server: SED\r\n/
        s/$/Connection: Close\r\n/
        s/$/\r|/
    x

    # Some rewriting...
    g

    s/^|Request:GET \/ /|Request:GET \/index.html /

    # Handle static files
    /^|Request:GET \/index.html /{
        s/\(.*|||ResponseHeaders:[^|]*\n\)\r|/\1Content-Type: text\/html; charset=utf-8\r\n\r|/
        s/.*|||ResponseHeaders:\(.*\)|/\1/;p
        r static/index.html
        q
    }
    /^|Request:GET \/favicon.ico /{
        s/\(.*|||ResponseHeaders:[^|]*\n\)\r|/\1Content-Type: image\/x-icon\r\n\r|/
        s/.*|||ResponseHeaders:\(.*\)|/\1/;p
        r static/favicon.ico
        q
    }
    /^|Request:GET \/style.css /{
        s/\(.*|||ResponseHeaders:[^|]*\n\)\r|/\1Content-Type: text\/css\r\n\r|/
        s/.*|||ResponseHeaders:\(.*\)|/\1/;p
        r static/style.css
        q
    }
    /^|Request:GET \/game\/level\([1-9]\|[1-8][0-9]\|90\)\/\(\([2-9]\|[1-9][0-9]\+\)\?[lrud]\)* /{
        s/\(.*|||ResponseHeaders:[^|]*\n\)\r|/\1Content-Type: text\/html; charset=utf-8\r\n\r|/
        s/.*|||ResponseHeaders:\(.*\)|/\1/;p
        bgame
    }
    # Default to 404
        s/\(.*|||ResponseHeaders:[^|]*\n\)\r\n|/\1Content-Type: text\/html; charset=utf-8\r\n|/
        s/\(ResponseHeaders:HTTP\/1.0\) 200 OK/\1 404 Not found/
        s/.*|||ResponseHeaders:\(.*\)|/\1/;p
        r static/404.html
        q
    q
}

d

:game

i\
<!DOCTYPE html>\
<html\
><head\
    ><title>SED Sokoban<\/title\
    ><link rel="shortcut icon" type="image/x-icon" href="/favicon.ico"\
    ><link rel="stylesheet" type="text/css" href="/style.css"\
/></head\
><body>\
<h1>SED Sokoban<\/h1>

# Transform from usual save format to our format
g
s/|Request:GET \/game\/level\([0-9]*\)\/\([ldur0-9]*\) .*/\2/
y/ldur/hjkl/
G
s/^\([hjkl0-9]*\)\n\(|Request:GET \/game\/level[0-9]*\/\)\([ldur0-9]*\) \(.*\)/\2\1 \4/
h

# Decompress request
s/|Request:GET \/game\/level\([0-9]*\)\/\([hjkl0-9]*\) .*/\2/
:loop_rle_deco1
/[0-9]\+[hjkl]/{
    s/[0-9]\+/|&|/
    :loop_rle_deco2

    /|0|./{brle_deco_ret}
    s/|\([hjkl]\)/|\1\1/

    :dec
    s/0\(_*|\)/_\1/
    tdec

    s/1\(_*|\)/0\1/;tdec_j
    s/2\(_*|\)/1\1/;tdec_j
    s/3\(_*|\)/2\1/;tdec_j
    s/4\(_*|\)/3\1/;tdec_j
    s/5\(_*|\)/4\1/;tdec_j
    s/6\(_*|\)/5\1/;tdec_j
    s/7\(_*|\)/6\1/;tdec_j
    s/8\(_*|\)/7\1/;tdec_j
    s/9\(_*|\)/8\1/;tdec_j
    :dec_j
    y/_/9/
    s/|0*/|/
    s/||/|0|/

    bloop_rle_deco2
    :rle_deco_ret
    s/|0|.//
    bloop_rle_deco1
}
G
s/^\([hjkl]*\)\n\(|Request:GET \/game\/level[0-9]*\/\)\([hjkl0-9]*\) \(.*\)/\2\1 \4/
h

# Count number of moves
    g
    s/|Request:GET \/game\/level\([0-9]*\)\/\([hjkl]*\) .*/\2/
    s/[hjkl]/#/g
    s/$/0/

    :count_loop1

    # Check whether we want to continue or not...
    /^#/!{bcount_end;}

    # replace all trailing 9s by _ (any other character except digits, could
    # be used)
    :count_d
    s/9\(_*\)$/_\1/
    tcount_d

    # incr last digit only.  The first line adds a most-significant
    # digit of 1 if we have to add a digit.
    #
    # The tn commands are not necessary, but make the thing
    # faster

    s/\(^\|#\)\(_*\)$/\11\2/; tcount_n
    s/8\(_*\)$/9\1/; tcount_n
    s/7\(_*\)$/8\1/; tcount_n
    s/6\(_*\)$/7\1/; tcount_n
    s/5\(_*\)$/6\1/; tcount_n
    s/4\(_*\)$/5\1/; tcount_n
    s/3\(_*\)$/4\1/; tcount_n
    s/2\(_*\)$/3\1/; tcount_n
    s/1\(_*\)$/2\1/; tcount_n
    s/0\(_*\)$/1\1/; tcount_n

    :count_n
    y/_/0/

    s/^[#]//
    tcount_loop1
    :count_end

    H;g
    s/\n\([0-9]*\)$/|Moves:\1|/
    h

# END: Count

# Status line
g
s/|Request:GET \/game\/level\([0-9]*\)\/\([hjkl]*\) .*|Moves:\([0-9]*\)|.*/\
<div>(level: \1, moves: \3)<\/div>/p

i\
<div class="main_wrapper">\
<div class="controls box">\
<h2>Controls<\/h2>\
<div class="inside"

# Controlling elements
g
s/|Request:GET \/game\/level\([0-9]*\)\/\([hjkl]*\) .*/\1 \2k \2h \2l \2j/

:loop_rle_comp1
/hh\|jj\|kk\|ll/{
    s/\(hh\|jj\|kk\|ll\)/|1|\1/

    :loop_rle_comp2
    /|\(hh\|jj\|kk\|ll\)/!{binc_ret}
    s/|[hjkl]/|/

    :inc
    s/9\(_*|\)/_\1/
    tinc

    s/|\(_*|\)/|1\1/; tinc_j
    s/8\(_*|\)/9\1/; tinc_j
    s/7\(_*|\)/8\1/; tinc_j
    s/6\(_*|\)/7\1/; tinc_j
    s/5\(_*|\)/6\1/; tinc_j
    s/4\(_*|\)/5\1/; tinc_j
    s/3\(_*|\)/4\1/; tinc_j
    s/2\(_*|\)/3\1/; tinc_j
    s/1\(_*|\)/2\1/; tinc_j
    s/0\(_*|\)/1\1/; tinc_j

    :inc_j
    y/_/0/
    bloop_rle_comp2

    :inc_ret
    s/|//g
    bloop_rle_comp1
}

# Use usual representation
y/hjkl/ldur/

s/\(.*\) \(.*\) \(.*\) \(.*\) \(.*\)/\
><a href="\/game\/level\1\/\2" accesskey="k" title="accesskey k">\&uarr;up<\/a>\
<a href="\/game\/level\1\/\3" accesskey="h" title="accesskey h">\&larr;left<\/a>|\
<a href="\/game\/level\1\/\4" accesskey="l" title="accesskey l">\&rarr;right<\/a>\
<a href="\/game\/level\1\/\5" accesskey="j" title="accesskey j">\&darr;down<\/a\
/
s/|\n/      /
p

i\
><\/div>\
<hr>\
<p>For better gameplay use <a href="http://en.wikipedia.org/wiki/Access_key">access keys</a> <kbd>h</kbd>, <kbd>j</kbd>, <kbd>k</kbd>, and <kbd>l</kbd>.\
Control via arrow keys was not added as they are not supproted as access keys and client-side SED scripting is not widely supported among today browsers.</p>\
<hr>\
<p>Tip: use browser history for your advantage.</p>\
<p>Tip: use bookmarks as save points.</p>\
<p>Tip: share your position with friends via URL.</p>\
<\/div>\
<div class="game box">\
<pre>

# Prepare data for game engine... level numbers and moves.
g
s/|Request:GET \/game\/level\([0-9]*\)\/.*/\1/

x
s/|Request:GET \/game\/level\([0-9]*\)\/\([hjkl]*\) .*/\2/
x

# loading map

# --------------------
  /^1$/s/.*/\
SED Sokoban - LEVEL 1\
\
     %%%%%            \
     %   %            \
     %o  %            \
   %%%  o%%           \
   %  o o %           \
 %%% % %% %   %%%%%%  \
 %   % %% %%%%%  ..%  \
 % o  o          ..%  \
 %%%%% %%% %@%%  ..%  \
     %     %%%%%%%%%  \
     %%%%%%%          \
\
\
\
\
\
\
/
# --------------------
  /^2$/s/.*/\
SED Sokoban - LEVEL 2\
\
 %%%%%%%%%%%%         \
 %..  %     %%%       \
 %..  % o  o  %       \
 %..  %o%%%%  %       \
 %..    @ %%  %       \
 %..  % %  o %%       \
 %%%%%% %%o o %       \
   % o  o o o %       \
   %    %     %       \
   %%%%%%%%%%%%       \
\
\
\
\
\
\
\
/
# --------------------
  /^3$/s/.*/\
SED Sokoban - LEVEL 3\
\
         %%%%%%%%     \
         %     @%     \
         % o%o %%     \
         % o  o%      \
         %%o o %      \
 %%%%%%%%% o % %%%    \
 %....  %% o  o  %    \
 %%...    o  o   %    \
 %....  %%%%%%%%%%    \
 %%%%%%%%             \
\
\
\
\
\
\
\
/
# --------------------
  /^4$/s/.*/\
SED Sokoban - LEVEL 4\
\
            %%%%%%%%  \
            %  ....%  \
 %%%%%%%%%%%%  ....%  \
 %    %  o o   ....%  \
 % ooo%o  o %  ....%  \
 %  o     o %  ....%  \
 % oo %o o o%%%%%%%%  \
 %  o %     %         \
 %% %%%%%%%%%         \
 %    %    %%         \
 %     o   %%         \
 %  oo%oo  @%         \
 %    %    %%         \
 %%%%%%%%%%%          \
\
\
\
/
# --------------------
  /^5$/s/.*/\
SED Sokoban - LEVEL 5\
\
         %%%%%        \
         %   %%%%%    \
         % %o%%  %    \
         %     o %    \
 %%%%%%%%% %%%   %    \
 %....  %% o  o%%%    \
 %....    o oo %%     \
 %....  %%o  o @%     \
 %%%%%%%%%  o  %%     \
         % o o  %     \
         %%% %% %     \
           %    %     \
           %%%%%%     \
\
\
\
\
/
# --------------------
  /^6$/s/.*/\
SED Sokoban - LEVEL 6\
\
 %%%%%%  %%%          \
 %..  % %%@%%         \
 %..  %%%   %         \
 %..     oo %         \
 %..  % % o %         \
 %..%%% % o %         \
 %%%% o %o  %         \
    %  o% o %         \
    % o  o  %         \
    %  %%   %         \
    %%%%%%%%%         \
\
\
\
\
\
\
/
# --------------------
  /^7$/s/.*/\
SED Sokoban - LEVEL 7\
\
        %%%%%         \
  %%%%%%%   %%        \
 %% % @%% oo %        \
 %    o      %        \
 %  o  %%%   %        \
 %%% %%%%%o%%%        \
 % o  %%% ..%         \
 % o o o ...%         \
 %    %%%...%         \
 % oo % %...%         \
 %  %%% %%%%%         \
 %%%%                 \
\
\
\
\
\
/
# --------------------
  /^8$/s/.*/\
SED Sokoban - LEVEL 8\
\
   %%%%               \
   %  %%%%%%%%%%%     \
   %    o   o o %     \
   % o% o %  o  %     \
   %  o o  %    %     \
 %%% o% %  %%%% %     \
 %@%o o o  %%   %     \
 %    o %o%   % %     \
 %   o    o o o %     \
 %%%%%  %%%%%%%%%     \
   %      %           \
   %      %           \
   %......%           \
   %......%           \
   %......%           \
   %%%%%%%%           \
\
/
# --------------------
  /^9$/s/.*/\
SED Sokoban - LEVEL 9\
\
           %%%%%%%    \
           %  ...%    \
       %%%%%  ...%    \
       %      . .%    \
       %  %%  ...%    \
       %% %%  ...%    \
      %%% %%%%%%%%    \
      % ooo %%        \
  %%%%%  o o %%%%%    \
 %%   %o o   %   %    \
 %@ o  o    o  o %    \
 %%%%%% oo o %%%%%    \
      %      %        \
      %%%%%%%%        \
\
\
\
/
# --------------------
  /^10$/s/.*/\
SED Sokoban - LEVEL 10\
\
  %%%  %%%%%%%%%%%%%  \
 %%@%%%%       %   %  \
 % oo   oo  o o ...%  \
 %  ooo%    o  %...%  \
 % o   % oo oo %...%  \
 %%%   %  o    %...%  \
 %     % o o o %...%  \
 %    %%%%%% %%%...%  \
 %% %  %  o o  %...%  \
 %  %% % oo o o%%..%  \
 % ..% %  o      %.%  \
 % ..% % ooo ooo %.%  \
 %%%%% %       % %.%  \
     % %%%%%%%%% %.%  \
     %           %.%  \
     %%%%%%%%%%%%%%%  \
\
/
# --------------------
  /^11$/s/.*/\
SED Sokoban - LEVEL 11\
\
           %%%%       \
      %%%% %  %       \
    %%% @%%%o %       \
   %%      o  %       \
  %%  o oo%% %%       \
  %  %o%%     %       \
  % % o oo % %%%      \
  %   o %  % o %%%%%  \
 %%%%    %  oo %   %  \
 %%%% %% o         %  \
 %.    %%%  %%%%%%%%  \
 %.. ..% %%%%         \
 %...%.%              \
 %.....%              \
 %%%%%%%              \
\
\
/
# --------------------
  /^12$/s/.*/\
SED Sokoban - LEVEL 12\
\
 %%%%%%%%%%%%%%%%     \
 %              %     \
 % % %%%%%%     %     \
 % %  o o o o%  %     \
 % %   o@o   %% %%    \
 % %  o o o%%%...%    \
 % %   o o  %%...%    \
 % %%%ooo o %%...%    \
 %     % %% %%...%    \
 %%%%%   %% %%...%    \
     %%%%%     %%%    \
         %     %      \
         %%%%%%%      \
\
\
\
\
/
# --------------------
  /^13$/s/.*/\
SED Sokoban - LEVEL 13\
\
    %%%%%%%%%         \
   %%   %%  %%%%%     \
 %%%     %  %    %%%  \
 %  o %o %  %  ... %  \
 % % o%@o%% % %.%. %  \
 %  % %o  %    . . %  \
 % o    o % % %.%. %  \
 %   %%  %%o o . . %  \
 % o %   %  %o%.%. %  \
 %% o  o   o  o... %  \
  %o %%%%%%    %%  %  \
  %  %    %%%%%%%%%%  \
  %%%%                \
\
\
\
\
/
# --------------------
  /^14$/s/.*/\
SED Sokoban - LEVEL 14\
\
        %%%%%%%       \
  %%%%%%%     %       \
  %     % o@o %       \
  %oo %   %%%%%%%%%   \
  % %%%......%%   %   \
  %   o......%% % %   \
  % %%%......     %   \
 %%   %%%% %%% %o%%   \
 %  %o   %  o  % %    \
 %  o ooo  % o%% %    \
 %   o o %%%oo % %    \
 %%%%%     o   % %    \
     %%% %%%   % %    \
       %     %   %    \
       %%%%%%%%  %    \
              %%%%    \
\
/
# --------------------
  /^15$/s/.*/\
SED Sokoban - LEVEL 15\
\
    %%%%%%%%          \
    %   %  %          \
    %  o   %          \
  %%% %o   %%%%       \
  %  o  %%o   %       \
  %  % @ o % o%       \
  %  %      o %%%%    \
  %% %%%%o%%     %    \
  % o%.....% %   %    \
  %  o..**. o% %%%    \
 %%  %.....%   %      \
 %   %%% %%%%%%%      \
 % oo  %  %           \
 %  %     %           \
 %%%%%%   %           \
      %%%%%           \
\
/
# --------------------
  /^16$/s/.*/\
SED Sokoban - LEVEL 16\
\
 %%%%%                \
 %   %%               \
 %    %  %%%%         \
 % o  %%%%  %         \
 %  oo o   o%         \
 %%%@ %o    %%        \
  %  %%  o o %%       \
  % o  %% %% .%       \
  %  %o%%o  %.%       \
  %%%   o..%%.%       \
   %    %.*...%       \
   % oo %.....%       \
   %  %%%%%%%%%       \
   %  %               \
   %%%%               \
\
\
/
# --------------------
  /^17$/s/.*/\
SED Sokoban - LEVEL 17\
\
    %%%%%%%%%%        \
    %..  %   %        \
    %..      %        \
    %..  %  %%%%      \
   %%%%%%%  %  %%     \
   %            %     \
   %  %  %%  %  %     \
 %%%% %%  %%%% %%     \
 %  o  %%%%% %  %     \
 % % o  o  % o  %     \
 % @o  o   %   %%     \
 %%%% %% %%%%%%%      \
    %    %            \
    %%%%%%            \
\
\
\
/
# --------------------
  /^18$/s/.*/\
SED Sokoban - LEVEL 18\
\
      %%%%%%%%%%%     \
      %  .  %   %     \
      % %.    @ %     \
  %%%%% %%..% %%%%    \
 %%  % ..%%%     %%%  \
 % o %...   o %  o %  \
 %    .. %%  %% %% %  \
 %%%%o%%o% o %   % %  \
   %% %    %o oo % %  \
   %  o % %  % o%% %  \
   %               %  \
   %  %%%%%%%%%%%  %  \
   %%%%         %%%%  \
\
\
\
\
/
# --------------------
  /^19$/s/.*/\
SED Sokoban - LEVEL 19\
\
   %%%%%%             \
   %   @%%%%          \
 %%%%% o   %          \
 %   %%    %%%%       \
 % o %  %%    %       \
 % o %  %%%%% %       \
 %% o  o    % %       \
 %% o o %%% % %       \
 %% %  o  % % %       \
 %% % %o%   % %       \
 %% %%%   % % %%%%%%  \
 %  o  %%%% % %....%  \
 %    o    o   ..%.%  \
 %%%%o  o% o   ....%  \
 %       %  %% ....%  \
 %%%%%%%%%%%%%%%%%%%  \
\
/
# --------------------
  /^20$/s/.*/\
SED Sokoban - LEVEL 20\
\
     %%%%%%%%%%       \
 %%%%%        %%%%    \
 %     %   o  %@ %    \
 % %%%%%%%o%%%%  %%%  \
 % %    %% %  %o ..%  \
 % % o     %  %  %.%  \
 % % o  %     %o ..%  \
 % %  %%% %%     %.%  \
 % %%%  %  %  %o ..%  \
 % %    %  %%%%  %.%  \
 % %o   o  o  %o ..%  \
 %    o % o o %  %.%  \
 %%%% o%%%    %o ..%  \
    %    oo %%%....%  \
    %      %% %%%%%%  \
    %%%%%%%%          \
\
/
# --------------------
  /^21$/s/.*/\
SED Sokoban - LEVEL 21\
\
 %%%%%%%%%            \
 %       %            \
 %       %%%%         \
 %% %%%% %  %         \
 %% %@%%    %         \
 % ooo o  oo%         \
 %  % %% o  %         \
 %  % %%  o %%%%      \
 %%%%  ooo o%  %      \
  %   %%   ....%      \
  % %   % %.. .%      \
  %   % % %%...%      \
  %%%%% o  %...%      \
      %%   %%%%%      \
       %%%%%          \
\
\
/
# --------------------
  /^22$/s/.*/\
SED Sokoban - LEVEL 22\
\
 %%%%%%     %%%%      \
 %    %%%%%%%  %%%%%  \
 %   o%  %  o  %   %  \
 %  o  o  o % o o  %  \
 %%o o   % @% o    %  \
 %  o %%%%%%%%%%% %%  \
 % %   %.......% o%   \
 % %%  % ......%  %   \
 % %   o........o %   \
 % % o %.... ..%  %   \
 %  o o%%%%o%%%% o%   \
 % o   %%% o   o  %%  \
 % o     o o  o    %  \
 %% %%%%%% o %%%%% %  \
 %         %       %  \
 %%%%%%%%%%%%%%%%%%%  \
\
/
# --------------------
  /^23$/s/.*/\
SED Sokoban - LEVEL 23\
\
     %%%%%%%          \
     %  %  %%%%       \
 %%%%% o%o %  %%      \
 %.. %  %  %   %      \
 %.. % o%o %  o%%%%   \
 %.  %     %o  %  %   \
 %..   o%  % o    %   \
 %..@%  %o %o  %  %   \
 %.. % o%     o%  %   \
 %.. %  %oo%o  %  %%  \
 %.. % o%  %  o%o  %  \
 %.. %  %  %   %   %  \
 %%. %%%%  %%%%%   %  \
  %%%%  %%%%   %%%%%  \
\
\
\
/
# --------------------
  /^24$/s/.*/\
SED Sokoban - LEVEL 24\
\
 %%%%%%%%%%%%%%%      \
 %..........  .%%%%   \
 %..........oo.%  %   \
 %%%%%%%%%%%o %   %%  \
 %      o  o     o %  \
 %% %%%%   %  o %  %  \
 %      %   %%  % %%  \
 %  o%  % %%  %%% %%  \
 % o %o%%%    %%% %%  \
 %%%  o %  %  %%% %%  \
 %%%    o %% %  % %%  \
  % o  %  o  o o   %  \
  %  o  o%ooo  %   %  \
  %  %  o      %%%%%  \
  % @%%  %  %  %      \
  %%%%%%%%%%%%%%      \
\
/
# --------------------
  /^25$/s/.*/\
SED Sokoban - LEVEL 25\
\
 %%%%                 \
 %  %%%%%%%%%%%%%%    \
 %  %   ..%......%    \
 %  % % %%%%% ...%    \
 %%o%    ........%    \
 %   %%o%%%%%%  %%%%  \
 % o %     %%%%%%@ %  \
 %%o % o   %%%%%%  %  \
 %  o %ooo%%       %  \
 %      %    %o%o%%%  \
 % %%%% %ooooo    %   \
 % %    o     %   %   \
 % %   %%        %%%  \
 % %%%%%%o%%%%%% o %  \
 %        %    %   %  \
 %%%%%%%%%%    %%%%%  \
\
/
# --------------------
  /^26$/s/.*/\
SED Sokoban - LEVEL 26\
\
  %%%%%%%             \
  %  %  %%%%%         \
 %%  %  %...%%%       \
 %  o%  %...  %       \
 % o %oo ...  %       \
 %  o%  %... .%       \
 %   % o%%%%%%%%      \
 %%o       o o %      \
 %%  %  oo %   %      \
  %%%%%%  %%oo@%      \
       %      %%      \
       %%%%%%%%       \
\
\
\
\
\
/
# --------------------
  /^27$/s/.*/\
SED Sokoban - LEVEL 27\
\
  %%%%%%%%%%%%%%%%%   \
  %...   %    %   %%  \
 %%.....  o%% % %o %  \
 %......%  o  %    %  \
 %......%  %  % %  %  \
 %%%%%%%%% o  o o  %  \
   %     %o%%o %%o%%  \
  %%   o    % o    %  \
  %  %% %%% %  %%o %  \
  % o oo     o  o  %  \
  % o    o%%o %%%%%%  \
  %%%%%%%  @ %%       \
        %%%%%%        \
\
\
\
\
/
# --------------------
  /^28$/s/.*/\
SED Sokoban - LEVEL 28\
\
          %%%%%       \
      %%%%%   %       \
     %% o  o  %%%%    \
 %%%%% o  o o %%.%    \
 %       oo  %%..%    \
 %  %%%%%% %%%.. %    \
 %% %  %    %... %    \
 % o   %    %... %    \
 %@ %o %% %%%%...%    \
 %%%%  o oo  %%..%    \
    %%  o o  o...%    \
     % oo  o %  .%    \
     %   o o  %%%%    \
     %%%%%%   %       \
          %%%%%       \
\
\
/
# --------------------
  /^29$/s/.*/\
SED Sokoban - LEVEL 29\
\
 %%%%%                \
 %   %%               \
 % o  %%%%%%%%%       \
 %% % %       %%%%%%  \
 %% %   o%o%@  %   %  \
 %  %      o %   o %  \
 %  %%% %%%%%%%%% %%  \
 %  %% ..*..... % %%  \
 %% %% *.*..*.* % %%  \
 % o%%%%%%%%%% %%o %  \
 %  o   o  o    o  %  \
 %  %   %   %   %  %  \
 %%%%%%%%%%%%%%%%%%%  \
\
\
\
\
/
# --------------------
  /^30$/s/.*/\
SED Sokoban - LEVEL 30\
\
        %%%%%%%%%%%   \
        %   %     %   \
 %%%%%  %     o o %   \
 %   %%%%% o%% % %%   \
 % o %%   % %% o  %   \
 % o  @oo % %%ooo %   \
 %% %%%   % %%    %   \
 %% %   %%% %%%%%o%   \
 %% %     o  %....%   \
 %  %%% %% o %....%%  \
 % o   o %   %..o. %  \
 %  %% o %  %%.... %  \
 %%%%%   %%%%%%...%%  \
     %%%%%    %%%%%   \
\
\
\
/
# --------------------
  /^31$/s/.*/\
SED Sokoban - LEVEL 31\
\
   %%%%               \
   %  %%%%%%%%%       \
  %%  %%  %   %       \
  %  o% o@o   %%%%    \
  %o  o  % o o%  %%   \
 %%  o%% %o o     %   \
 %  %  % %   ooo  %   \
 % o    o  o%% %%%%   \
 % o o %o%  %  %      \
 %%  %%%  %%%o %      \
  %  %....     %      \
  %%%%......%%%%      \
    %....%%%%         \
    %...%%            \
    %...%             \
    %%%%%             \
\
/
# --------------------
  /^32$/s/.*/\
SED Sokoban - LEVEL 32\
\
       %%%%           \
   %%%%%  %           \
  %%     o%           \
 %% o  %% %%%         \
 %@o o % o  %         \
 %%%% %%   o%         \
  %....%o o %         \
  %....%   o%         \
  %....  oo %%        \
  %... % o   %        \
  %%%%%%o o  %        \
       %   %%%        \
       %o %%%         \
       %  %           \
       %%%%           \
\
\
/
# --------------------
  /^33$/s/.*/\
SED Sokoban - LEVEL 33\
\
  %%%%%%%%%%%         \
  %     %%  %         \
  %   o   o %         \
 %%%% %% oo %         \
 %   o %    %         \
 % ooo % %%%%         \
 %   % % o %%         \
 %  %  %  o %         \
 % o% o%    %         \
 %   ..% %%%%         \
 %%%%.. o %@%         \
 %.....% o% %         \
 %%....%  o %         \
  %%..%%    %         \
   %%%%%%%%%%         \
\
\
/
# --------------------
  /^34$/s/.*/\
SED Sokoban - LEVEL 34\
\
  %%%%%%%%%           \
  %....   %%          \
  %.%.%  o %%         \
 %%....% % @%%        \
 % ....%  %  %%       \
 %     %o %%o %       \
 %% %%%  o    %       \
  %o  o o o%  %       \
  % %  o o %% %       \
  %  %%%  %%  %       \
  %    %% %% %%       \
  %  o %  o  %        \
  %%%o o   %%%        \
    %  %%%%%          \
    %%%%              \
\
\
/
# --------------------
  /^35$/s/.*/\
SED Sokoban - LEVEL 35\
\
 %%%%%%%%%%%% %%%%%%  \
 %   %    % %%%....%  \
 %   oo%   @  .....%  \
 %   % %%%   % ....%  \
 %% %% %%%  %  ....%  \
  % o o     % % %%%%  \
  %  o o%%  %      %  \
 %%%% %  %%%% % %% %  \
 %  % %o   %% %    %  \
 % o  o  % %% %   %%  \
 % % o o    % %   %   \
 %  o %% %% % %%%%%   \
 % oo     oo  %       \
 %% %% %%% o  %       \
  %    % %    %       \
  %%%%%% %%%%%%       \
\
/
# --------------------
  /^36$/s/.*/\
SED Sokoban - LEVEL 36\
\
             %%%%%    \
 %%%%%  %%%%%%   %    \
 %   %%%%  o o o %    \
 % o   %% %% %%  %%   \
 %   o o     o  o %   \
 %%% o  %% %%     %%  \
   % %%%%% %%%%%oo %  \
  %%o%%%%% @%%     %  \
  % o  %%%o%%% o  %%  \
  % o  %   %%%  %%%   \
  % oo o %   oo %     \
  %     %   %%  %     \
  %%%%%%%.. .%%%      \
     %.........%      \
     %.........%      \
     %%%%%%%%%%%      \
\
/
# --------------------
  /^37$/s/.*/\
SED Sokoban - LEVEL 37\
\
 %%%%%%%%%%%          \
 %......   %%%%%%%%%  \
 %......   %  %%   %  \
 %..%%% o    o     %  \
 %... o o %   %%   %  \
 %...%o%%%%%    %  %  \
 %%%    %   %o  %o %  \
   %  oo o o  o%%  %  \
   %  o   %o%o %%o %  \
   %%% %% %    %%  %  \
    %  o o %% %%%%%%  \
    %    o  o  %      \
    %%   % %   %      \
     %%%%%@%%%%%      \
         %%%          \
\
\
/
# --------------------
  /^38$/s/.*/\
SED Sokoban - LEVEL 38\
\
       %%%%           \
 %%%%%%% @%           \
 %     o  %           \
 %   o%% o%           \
 %%o%...% %           \
  % o...  %           \
  % %. .% %%          \
  %   % %o %          \
  %o  o    %          \
  %  %%%%%%%          \
  %%%%                \
\
\
\
\
\
\
/
# --------------------
  /^39$/s/.*/\
SED Sokoban - LEVEL 39\
\
              %%%%%%  \
  %%%%%%%%%%%%%....%  \
 %%   %%     %%....%  \
 %  oo%%  o @%%....%  \
 %      oo o%  ....%  \
 %  o %% oo % % ...%  \
 %  o %% o  %  ....%  \
 %% %%%%% %%% %%.%%%  \
 %%   o  o %%   .  %  \
 % o%%%  % %%%%% %%%  \
 %   o   %       %    \
 %  o %o o o%%%  %    \
 % ooo% o   % %%%%    \
 %    %  oo %         \
 %%%%%%   %%%         \
      %%%%%           \
\
/
# --------------------
  /^40$/s/.*/\
SED Sokoban - LEVEL 40\
\
     %%%%%%%%%%%%     \
     %          %%    \
     %  % %oo o  %    \
     %o %o%  %% @%    \
    %% %% % o % %%    \
    %   o %o  % %     \
    %   % o   % %     \
    %% o o   %% %     \
    %  %  %%  o %     \
    %    %% oo% %     \
 %%%%%%oo   %   %     \
 %....%  %%%%%%%%     \
 %.%... %%            \
 %....   %            \
 %....   %            \
 %%%%%%%%%            \
\
/
# --------------------
  /^41$/s/.*/\
SED Sokoban - LEVEL 41\
\
            %%%%%     \
           %%   %%    \
          %%     %    \
         %%  oo  %    \
        %% oo  o %    \
        % o    o %    \
 %%%%   %   oo %%%%%  \
 %  %%%%%%%% %%    %  \
 %.            ooo@%  \
 %.% %%%%%%% %%   %%  \
 %.% %%%%%%%. %o o%%  \
 %........... %    %  \
 %%%%%%%%%%%%%%  o %  \
              %%  %%  \
               %%%%   \
\
\
/
# --------------------
  /^42$/s/.*/\
SED Sokoban - LEVEL 42\
\
      %%%%%%%%        \
   %%%%      %%%%%%   \
   %    %% o o   @%   \
   % %% %%o%o o o%%   \
 %%% ......%  oo %%   \
 %   ......%  %   %   \
 % % ......%o  o  %   \
 % %o...... oo% o %   \
 %   %%% %%%o  o %%   \
 %%%  o  o  o  o %    \
   %  o  o  o  o %    \
   %%%%%%   %%%%%%    \
        %%%%%         \
\
\
\
\
/
# --------------------
  /^43$/s/.*/\
SED Sokoban - LEVEL 43\
\
         %%%%%%%      \
     %%%%%  %  %%%%   \
     %   %   o    %   \
  %%%% %oo %% %%  %   \
 %%      % %  %% %%%  \
 %  %%% o%o  o  o  %  \
 %...    % %%  %   %  \
 %...%    @ % %%% %%  \
 %...%  %%%  o  o  %  \
 %%%%%%%% %%   %   %  \
           %%%%%%%%%  \
\
\
\
\
\
\
/
# --------------------
  /^44$/s/.*/\
SED Sokoban - LEVEL 44\
\
  %%%%%               \
  %   %               \
  % % %%%%%%%         \
  %      o@%%%%%%     \
  % o %%o %%%   %     \
  % %%%% o    o %     \
  % %%%%% %  %o %%%%  \
 %%  %%%% %%o      %  \
 %  o%  o  % %% %% %  \
 %         % %...% %  \
 %%%%%%  %%%  ...  %  \
      %%%% % %...% %  \
           % %%% % %  \
           %       %  \
           %%%%%%%%%  \
\
\
/
# --------------------
  /^45$/s/.*/\
SED Sokoban - LEVEL 45\
\
 %%%%% %%%%           \
 %...% %  %%%%        \
 %...%%%  o  %        \
 %....%% o  o%%%      \
 %%....%%   o  %      \
 %%%... %% o o %      \
 % %%    %  o  %      \
 %  %% % %%% %%%%     \
 % o % %o  o    %     \
 %  o @ o    o  %     \
 %   % o oo o %%%     \
 %  %%%%%%  %%%       \
 % %%    %%%%         \
 %%%                  \
\
\
\
/
# --------------------
  /^46$/s/.*/\
SED Sokoban - LEVEL 46\
\
 %%%%%%%%%%           \
 %        %%%%        \
 % %%%%%% %  %%       \
 % % o o o  o %       \
 %       %o   %       \
 %%%o  oo%  %%%       \
   %  %% % o%%        \
   %%o%   o @%        \
    %  o o %%%        \
    % %   o  %        \
    % %%   % %        \
   %%  %%%%% %        \
   %         %        \
   %.......%%%        \
   %.......%          \
   %%%%%%%%%          \
\
/
# --------------------
  /^47$/s/.*/\
SED Sokoban - LEVEL 47\
\
          %%%%        \
  %%%%%%%%%  %%       \
 %%  o      o %%%%%   \
 %   %% %%   %%...%   \
 % %oo o oo%o%%...%   \
 % %   @   %   ...%   \
 %  o% %%%oo   ...%   \
 % o  oo  o %%....%   \
 %%%o       %%%%%%%   \
   %  %%%%%%%         \
   %%%%               \
\
\
\
\
\
\
/
# --------------------
  /^48$/s/.*/\
SED Sokoban - LEVEL 48\
\
   %%%%%%%%%          \
   %*.*%*.*%          \
   %.*.*.*.%          \
   %*.*.*.*%          \
   %.*.*.*.%          \
   %*.*.*.*%          \
   %%%   %%%          \
     %   %            \
 %%%%%% %%%%%%        \
 %           %        \
 % o o o o o %        \
 %% o o o o %%        \
  %o o o o o%         \
  %   o@o   %         \
  %  %%%%%  %         \
  %%%%   %%%%         \
\
/
# --------------------
  /^49$/s/.*/\
SED Sokoban - LEVEL 49\
\
        %%%%          \
        %  %%         \
        %   %%        \
        % oo %%       \
      %%%o  o %%      \
   %%%%    o   %      \
 %%%  % %%%%%  %      \
 %    % %....o %      \
 % %   o ....% %      \
 %  o % %.*..% %      \
 %%%  %%%% %%% %      \
   %%%% @o  %%o%%     \
      %%% o     %     \
        %  %%   %     \
        %%%%%%%%%     \
\
\
/
# --------------------
  /^50$/s/.*/\
SED Sokoban - LEVEL 50\
\
       %%%%%%%%%%%%   \
      %%..    %   %   \
     %%..* o    o %   \
    %%..*.% % % o%%   \
    %..*.% % % o  %   \
 %%%%...%  %    % %   \
 %  %% %          %   \
 % @o o %%%  %   %%   \
 % o   o   % %   %    \
 %%%oo   % % % % %    \
   %   o   % % %%%%%  \
   % o% %%%%%      %  \
   %o   %   %    % %  \
   %  %%%   %%     %  \
   %  %      %    %%  \
   %%%%      %%%%%%   \
\
/
# --------------------
  /^51$/s/.*/\
SED Sokoban - LEVEL 51\
\
  %%%%%%%%%           \
  %       %           \
  % o oo o%           \
 %%% %  o %           \
 %.%   oo %%          \
 %.%%%   o %          \
 %.%. o %% %%%%       \
 %...  o%% o  %       \
 %...o   o    %       \
 %..%%%o%%% %@%       \
 %..% %     %%%       \
 %%%% %%%%%%%         \
\
\
\
\
\
/
# --------------------
  /^52$/s/.*/\
SED Sokoban - LEVEL 52\
\
            %%%%%%%%  \
            %......%  \
    %%%%    %......%  \
    %  %%%%%%%%%...%  \
    % o   o    %...%  \
    %  % % % % %   %  \
 %%%%% % % %@% %   %  \
 %   % %%% %%% %% %%  \
 %    o % o o o % %   \
 % ooo  o   %     %   \
 %   % %%%o%%%o%% %   \
 %%% %  o   %     %   \
  %% o  % o o o %%%   \
  %  % %%% %%% %%     \
  % o          %      \
  %  %%%%%%%%%%%      \
  %%%%                \
/
# --------------------
  /^53$/s/.*/\
SED Sokoban - LEVEL 53\
\
 %%%%%%%%%%%%%%%%%%   \
 %                %%  \
 % o%   o %%  o    %  \
 %    o%%%    % oo %  \
 %.%%%     o o %%  %% \
 %...%  %  %    %o  % \
 %..%%oo%%%% o  %   % \
 %...%      o %%  %%% \
 %...o  %%%  %    % % \
 %%..  o%  %%   %%@ % \
  %%.%              % \
   %%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^54$/s/.*/\
SED Sokoban - LEVEL 54\
\
 %%%%%%%%%%%%%%%%%%%% \
 %   %    %   %   %@% \
 % o      o   o   % % \
 %% %%%..%% %%%     % \
 %   %....%o%  o%%% % \
 % o %....%  o  o o % \
 %   %....% % % o o % \
 %   %%..%%   %o%   % \
 %%o%%    %%  %  %o%% \
 %   o  o     %  %  % \
 %   %    %   %     % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^55$/s/.*/\
SED Sokoban - LEVEL 55\
\
 %%%%%%%%%%%%%%%%%%%% \
 %    @%%      %   %% \
 %    %%    o    o %% \
 %  %%%....% % %  %%% \
 %   %....% % % o   % \
 %%% %...%  %       % \
 %%  %%.%     o   o % \
 %%  o o %%%  % % %%% \
 %% o       % % o   % \
 %%%% o  o% % % % o % \
 %%%%         % %  %% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^56$/s/.*/\
SED Sokoban - LEVEL 56\
\
 %%%%%%%%%%%%%%%%%%%% \
 %  %  %%    %   @%%% \
 %%    o    % o%%%  % \
 %%o% o %%o% o o    % \
 %   o%    o      %%% \
 % %%   o %%%  %....% \
 % % o% % % % %....%% \
 %    o o %  %....%%% \
 %%o %%%  o %....%%%% \
 %  % o        %%%%%% \
 %      % %    %%%%%% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^57$/s/.*/\
SED Sokoban - LEVEL 57\
\
 %%%%%%%%%%%%%%%%%%%% \
 %@     %%%   %  %  % \
 % % %  %  o  o     % \
 %%%%%     % o o%o% % \
 %.%..%    %%o o    % \
 %.....    o   %   %% \
 %.....    %%%o%%o%%% \
 %.%..%    o    %   % \
 %%%%%     %  %o  o % \
 %%%%%  %  o    o o % \
 %%%%%  %  %  %  %  % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^58$/s/.*/\
SED Sokoban - LEVEL 58\
\
 %%%%%%%%%%%%%%%%%%%% \
 %%...   %% %    %  % \
 %....         o %% % \
 %....% % %o%%%o    % \
 %...%    %       % % \
 %%.%  %o %     o%% % \
 %  %  % o o %%%  o % \
 %     o  o %  % %% % \
 %% % %% %oo% o%  % % \
 %  %   o o %      %% \
 %    %     %  %   @% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^59$/s/.*/\
SED Sokoban - LEVEL 59\
\
 %%%%%%%%%%%%%%%%%%%% \
 %   %  %@% %%  %%%%% \
 % % %  o    o  %%%%% \
 % %    %%%%%% o  %%% \
 %   %  %....%  oo  % \
 %%o%%o%%....%      % \
 %      %....%%o%%o%% \
 %  oo  %....%      % \
 % o  o  %  %  %%%  % \
 %%%%%  o   o    o  % \
 %%%%% %    %  %   %% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^60$/s/.*/\
SED Sokoban - LEVEL 60\
\
 %%%%%%%%%%%%%%%%%%%% \
 % %     %          % \
 %       o  %% %%% %% \
 %%%%%  %%   o  o   % \
 %%..%%  % % o % %  % \
 %....  o     %%o% %% \
 %....  o%%%%%   %o%% \
 %%..% %  %   %  o  % \
 %%%.% %  o   o  % @% \
 %%  o  o %   %  %%%% \
 %%       %%%%%%%%%%% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^61$/s/.*/\
SED Sokoban - LEVEL 61\
\
 %%%%%%%%%%%%%%%%%%%% \
 %     %%%..%%%     % \
 % oo  %%%..%%%  o@ % \
 %  % %%......%  o  % \
 %     %......%  o  % \
 %%%%  %%%..%%%%%%o % \
 %   ooo %..%    %  % \
 % o%   o  o  oo %o % \
 %  %  %% o  %%  %  % \
 % o    o %% o    o % \
 %  %  %%    %%  %  % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^62$/s/.*/\
SED Sokoban - LEVEL 62\
\
 %%%%%%%%%%%%%%%%%%%% \
 %    %  % %  %  %  % \
 % @% % %% o   o   %% \
 %%%% %    %  % o   % \
 %    % %% %o %% %% % \
 %      o   o   o   % \
 %..%%%oo%% o%%o %% % \
 %..%.%  % o   o %  % \
 %....% oo   %%o %%%% \
 %....%  %%%%%      % \
 %...%%%        %%  % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^63$/s/.*/\
SED Sokoban - LEVEL 63\
\
 %%%%%%%%%%%%%%%%%%%% \
 %....%       %  %  % \
 %....% % o  o      % \
 %.... %%  o% % o%o % \
 %...%   o   o%  o  % \
 %..%%%%  % o   oo  % \
 %      %%%% %%%% %%% \
 %        %   %     % \
 % %%   %   o % o o % \
 % %%    o %% o  o  % \
 %     @%     %   % % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^64$/s/.*/\
SED Sokoban - LEVEL 64\
\
 %%%%%%%%%%%%%%%%%%%% \
 %....%%%           % \
 %....%%%%% %  %o% %% \
 %....%%%   %o  o   % \
 %....%%%    o  %oo%% \
 %%  %%%% o%  %o o  % \
 %%  %%%%  o  o  %  % \
 %@  %%%%o%%%o%% o  % \
 %%        %  %  o  % \
 %%   %%%  %  o  %%%% \
 %%%%%%%%  %  %     % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^65$/s/.*/\
SED Sokoban - LEVEL 65\
\
 %%%%%%%%%%%%%%%%%%%% \
 %     %     @%...%%% \
 %     %      %%...%% \
 % % % %%o%% %% ....% \
 %   o %   ooo  ....% \
 %%%o%%% oo  %%% %%.% \
 %     o  %    % %%%% \
 %  o  %  %%%  % %  % \
 %% %o%%    o  oo   % \
 %   o %%   %  % %  % \
 %     %    %  %    % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^66$/s/.*/\
SED Sokoban - LEVEL 66\
\
 %%%%%%%%%%%%%%%%%%%% \
 %     %  %...%@    % \
 % %       ....%    % \
 %  o  %   %....%   % \
 % %%o%%%% %%....%  % \
 % o   o  %  %...%  % \
 % oo %   %   % oo  % \
 %%%  ooo%   oo  o  % \
 % o  %  %    % o%  % \
 %   o%  %       o  % \
 %  %    %    %  %  % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^67$/s/.*/\
SED Sokoban - LEVEL 67\
\
 %%%%%%%%%%%%%%%%%%%% \
 %%%%%@%%%.%%...%%  % \
 %%%%%o  ..%...%    % \
 %%%%    ......%  o % \
 %%%  o %.....%% % %% \
 %%  oo% %%%%%  o o % \
 %% o% o    %%  oo  % \
 %%  %  %    % o  o % \
 %%   oo %%% %o%%   % \
 %% o%      o o  o %% \
 %%%    %    %    %%% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^68$/s/.*/\
SED Sokoban - LEVEL 68\
\
 %%%%%%%%%%%%%%%%%%%% \
 %@     %   %       % \
 %% %%% %%  %%%% % %% \
 %    % %  oo       % \
 %  % % % o % o %% %% \
 %     o %  %oo %   % \
 %  %%%  %      %% %% \
 %..%.% o %  o %    % \
 %..%.%  o % %% oo  % \
 %....%%   oo  o  % % \
 %.....%%        %  % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^69$/s/.*/\
SED Sokoban - LEVEL 69\
\
 %%%%%%%%%%%%%%%%%%%% \
 %  %      %   %   %% \
 % o% o o %%...o  o % \
 %  o  % %%....% o  % \
 % %% o %%....%   o % \
 % o    %....%% o   % \
 % o%%  %...%       % \
 %   ooo%%o%%  %%% %% \
 % % %  %   %  %    % \
 % o %  o  %%       % \
 %    %    %@       % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^70$/s/.*/\
SED Sokoban - LEVEL 70\
\
 %%%%%%%%%%%%%%%%%%%% \
 %  %  % %    %  %  % \
 %   o      o o     % \
 %% %  %o%%%o%%  %% % \
 %   o     o  %  o  % \
 % %%%o%%o%   % o   % \
 % %   o o  %%%%%% o% \
 % o  oo o  %@%.%...% \
 % %     %  % %.%...% \
 % %%%%%%%%%% %.....% \
 %            %.....% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^71$/s/.*/\
SED Sokoban - LEVEL 71\
\
 %%%%%%%%%%%%%%%%%%%% \
 %  %     %  %%    %% \
 % o%   o %     %%  % \
 % o  o  %..%     o % \
 % o o  %....%   % %% \
 % o%  %......%%% o % \
 %   %  %....%  %o  % \
 % o  %%%%..%   %   % \
 %% o   %% % % o  o%% \
 %%% o    o%@o o%   % \
 %%%%   %       %   % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^72$/s/.*/\
SED Sokoban - LEVEL 72\
\
 %%%%%%%%%%%%%%%%%%%% \
 %      ....%    %%%% \
 %      ....        % \
 % % %%%%%%%%%%     % \
 % %o   %      %%%..% \
 %  o   %oo%%%   %..% \
 % o %%% o   o   %..% \
 % o %   o o %  %%..% \
 %  %  oo % o %%   %% \
 %@%% o%  o  o     %% \
 %%       %%   %  %%% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^73$/s/.*/\
SED Sokoban - LEVEL 73\
\
 %%%%%%%%%%%%%%%%%%%% \
 %        %   %@ %  % \
 % oo  %oo% % %  %% % \
 %  % o o %oo %     % \
 %% %  %  % % %  %  % \
 %   %%       %     % \
 %   % o %   %   %  % \
 % o %o %   %  o %..% \
 %%o %  %%%%    %...% \
 %  o          %....% \
 %   %  %     %.....% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^74$/s/.*/\
SED Sokoban - LEVEL 74\
\
 %%%%%%%%%%%%%%%%%%%% \
 %     %   %%%%%    % \
 %% o  %   %%%%  o  % \
 %%%% oo   %..%  %  % \
 %  o  o  %%..%%%% %% \
 % o   %%%....   oo % \
 %  %o%   ....% % o % \
 % %  % o ..%%%o%   % \
 % %   o %..%   %%  % \
 %   o%  %%%%   % o%% \
 % %  %    @%      %% \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^75$/s/.*/\
SED Sokoban - LEVEL 75\
\
 %%%%%%%%%%%%%%%%%%%% \
 %   %   %    %   %@% \
 %   o  o     % o % % \
 %%o% o%%% %    oo% % \
 %  %  %.%%%  %o o  % \
 %  %o%....%  % %%% % \
 % o  %.....%%    % % \
 %%o  %.%....%oo o  % \
 %  %%%%%%..%% %  % % \
 %  o         o %%% % \
 %   %   %        % % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^76$/s/.*/\
SED Sokoban - LEVEL 76\
\
 %%%%%%%%%%%%%%%%%%%% \
 % % % %   %@%%   % % \
 %             o    % \
 %  %%o% %%%%% o % %% \
 %%    %%.....%  %  % \
 %%o%%o%.....%%%o%o % \
 %   % %%.....%  % %% \
 %  o    %%..%%  %  % \
 % o %   o   o  ooo % \
 %% o  o% %  %  o   % \
 %   %%   %  %      % \
 %%%%%%%%%%%%%%%%%%%% \
\
\
\
\
\
/
# --------------------
  /^77$/s/.*/\
SED Sokoban - LEVEL 77\
\
 %%%%%%%%%%%%%%%%%%%% \
 %    %%   %    %   % \
 %  o  o     %% o   % \
 %% %%%%%  .%%%%%% %% \
  % %%  %%....%%%% %% \
 %% %%o %%%..%%     % \
 %      %... .% o o % \
 % o %% %% . %%% %%%% \
 % % o    %.%% % %    \
 % o o %   .%%%% %%   \
 % %  %% % %%  %  %%  \
 %%%%%%%  o%%o   o %  \
       %%      o %@%  \
        %  %% %%%%%%  \
        %%%%%%%       \
\
\
/
# --------------------
  /^78$/s/.*/\
SED Sokoban - LEVEL 78\
\
        %%%%%%%%%%%   \
        %         %   \
        %    o o  %   \
 %%%%%% % o %%%%% %   \
 %    %%%%% o  %%o%   \
 %       o o      %   \
 %          %% %% %   \
 %    %%@%%%%% %% %   \
 %    %%%%   % %% %%  \
 %....%      % o   %  \
 %....%      %     %  \
 %%%%%%      %%%%%%%  \
\
\
\
\
\
/
# --------------------
  /^79$/s/.*/\
SED Sokoban - LEVEL 79\
\
 %%%%%%%%%%%%%        \
 %           %        \
 % %%% oo    %        \
 %   % o  o  %        \
 %  o%%%%o%%%%%%      \
 % o %%        %%%%%  \
 %  oo o        ...%  \
 %%% %% oo%     ...%  \
   % %%   %     ...%  \
   %      %     ...%  \
   %%%@%%%%%%%%%%%%%  \
     %%%              \
\
\
\
\
\
/
# --------------------
  /^80$/s/.*/\
SED Sokoban - LEVEL 80\
\
   %%%%%%%%%%%%%%%%%  \
 %%%@%%         ...%  \
 %    %         ...%  \
 % o  %         ...%  \
 % oo %         ...%  \
 %% o %%%o%%%%%%%%%%  \
  % %%%  o %          \
 %%   o  o %          \
 %  o %  o %          \
 % o  %    %          \
 %  o %    %          \
 %    %    %          \
 %%%%%%%%%%%          \
\
\
\
\
/
# --------------------
  /^81$/s/.*/\
SED Sokoban - LEVEL 81\
\
               %%%%%  \
      %%%%%%%%%%   %  \
      %        %   %  \
      %  o o    oo %  \
      % %%%%% %% o %  \
      %oo   %o%% o %  \
      % %%% % %%o  %  \
 %%%%%% %%% o o    %  \
 %....        %%   %  \
 %....        %%%%%%  \
 %....        %       \
 %%%%%%%%%%%@%%       \
           %%%        \
\
\
\
\
/
# --------------------
  /^82$/s/.*/\
SED Sokoban - LEVEL 82\
\
     %%%%%%           \
  %%%%    %           \
  %    %% %           \
  % o     %           \
 %%% %%%% %%%%%%%%    \
 %  o   o %%  ...%    \
 %   oo oo    ...%    \
 %    o  o%%  ...%    \
 %%@%% %% %%  ...%    \
  %%%  o  %%%%%%%%    \
  %   oo  %           \
  %    %  %           \
  %%%%%%%%%           \
\
\
\
\
/
# --------------------
  /^83$/s/.*/\
SED Sokoban - LEVEL 83\
\
 %%%%%%% %%%%%%%%%    \
 %     % %   %%  %    \
 % %%% % %   o   %    \
 % % o %%%   o   %    \
 %   oo      %%o %    \
 %    %%%%   %%  %    \
 %@%%%%%%%%%%%% %%    \
 %%%..    %%%%%o %    \
   %..    %%%%   %    \
   %..       oo  %    \
   %..    %%%% o %    \
   %..    %  %   %    \
   %%%%%%%%  %%%%%    \
\
\
\
\
/
# --------------------
  /^84$/s/.*/\
SED Sokoban - LEVEL 84\
\
 %%%%%%%              \
 %     %%%%%%%%%%     \
 %     %    %  %%     \
 % o   %   o o  %     \
 %  o  %  o %%  %     \
 % oo  %%o o    %     \
 %% %  %% %%%%%%%     \
 %% %  %%    ...%     \
 %  %o       ...%     \
 %   oo      ...%     \
 %     %%@%  ...%     \
 %%%%%%%%%%%%%%%%     \
\
\
\
\
\
/
# --------------------
  /^85$/s/.*/\
SED Sokoban - LEVEL 85\
\
 %%%%%%%%%%%%         \
 %      %   %%        \
 % o  o   %  %%%%%%   \
 %%%%  %%%%%      %   \
  %..  %     %%%% %   \
  %.%%%%  %%%%    %   \
  %....    %  o %%%%  \
  % ...%   % ooo%  %% \
 %%%.%%%% %%  o@o   % \
 %     %%%%% o %    % \
 % %.% o      o%%%o % \
 % %.%%%%%%%%  %  o % \
 % %..        %%  o % \
 % % %%%%%%% o % %  % \
 %   %     %       %% \
 %%%%%     %%%%%%%%%% \
\
/
# --------------------
  /^86$/s/.*/\
SED Sokoban - LEVEL 86\
\
 %%%%%%%%%%%%%%%%     \
 %       %@ %   %     \
 % % % % % o  oo%     \
 % %...% %ooo   %     \
 %  ...% % o  oo%%    \
 % %%.%% % %%    %    \
 % %...     o    %    \
 % %% %%%  %%%%%%%    \
 %    % %%%%          \
 %%%%%%               \
\
\
\
\
\
\
\
/
# --------------------
  /^87$/s/.*/\
SED Sokoban - LEVEL 87\
\
     %%%%%            \
  %%%%   %% %%%%%     \
  %  o    %%%   %     \
  % o@o o    o  %     \
  % %o%%%%%%%% %%     \
  % %  o  %     %     \
  % % o o % %   %     \
 %% %  o% % %%%%%     \
 %  %%    %     %     \
 %    o % %%%   %     \
 %%%%% %%  %....%     \
 %    o     ....%     \
 %         %....%     \
 %%%%%%%%%%%%%%%%     \
\
\
\
/
# --------------------
  /^88$/s/.*/\
SED Sokoban - LEVEL 88\
\
 %%%%%%%%%%%%%        \
 %........%%%%        \
 %...%%%% %  %%%%%    \
 %...%  %%%    o %    \
 %...oo     o o  %    \
 %  .%  o o% o  %%    \
 %...% %o%   o  %     \
 %.% % o   o    %     \
 %.  %o%%%o%%%%o%     \
 %%  %   o o    %     \
  %  %  o@o  %  %     \
  %  % %%%% o  o%     \
  %  %    %%%   %     \
  %  % oo % %%%%%     \
  %  %    %           \
  %%%%%%%%%           \
\
/
# --------------------
  /^89$/s/.*/\
SED Sokoban - LEVEL 89\
\
  %%%%%%%%%%%%%%%%%%  \
  %   o       ...%.%% \
  %       %%%%..... % \
  % %%%%%%%  %..... % \
  % %    o o %%....%% \
  % %  o % % %%%...%  \
  % % o@o o  %%%%% %  \
 %% %  o  o oo   o %  \
 %  %o% o%   % o%% %  \
 % %%    %% %% o % %  \
 % % o% o o  %     %  \
 % %         %%%%%%%  \
 % %%%%%%%%o%%   %    \
 %        %  o   %    \
 %%%%%%%%    %%%%%    \
        %%%  %        \
          %%%%        \
/
# --------------------
  /^90$/s/.*/\
SED Sokoban - LEVEL 90\
\
 %%%%%%%%%%%%%%%%%%%% \
 %..%    %          % \
 %.o  o  %oo  o%% o%% \
 %.o%  %%%  %% %%   % \
 %  % o %  oo   o   % \
 % %%%  % %  %o  %%%% \
 %  %% % o   %@ %   % \
 % o    o  %%.%%  o % \
 %  % o% o% o     %%% \
 %  %  %  %   %%%   % \
 %  %%%%%%%% %      % \
 %           %  %.%.% \
 %%o%%%%%%%%o%   ...% \
 %    .O  %    %%.%.% \
 % .O...O   o  .....% \
 %%%%%%%%%%%%%%%%%%%% \
                      \
/

/SED Soko/!{s/.*/there is no '&' level!/p;q;}

:map_loaded

s/^\n//

x
# here the party begins
:ini

# wipe trash
s/[^kjlh]//g

# -------------[ LEFT ]--------------------------

/^h/{

# del current move and save others
  s///;x

# clear path
  / @/{s//@ /;bx;}
# push load
  / o@/{s//o@ /;bx;}

# enter overdot
  /\.@/{s//! /;bx;}
# continue overdot
  /\.!/{s//!./;bx;}
# out overdot
  / !/{s//@./;bx;}

# enter load overdot
  /\.o@/{s//O@ /;bx;}
# enter overdot with load
  /\.O@/{s//O! /;bx;}
# continue overdot with load
  /\.O!/{s//O!./;bx;}
# out load overdot / enter overdot
  / O@/{s//o! /;bx;}
# out load overdot / continue overdot
  / O!/{s//o!./;bx;}
# out overdot with load
  / o!/{s//o@./;bx;}
# out overdot with load / enter overdot
  /\.o!/{s//O@./;bx;}

# can't pass
  bx

}


# -------------[ RIGHT ]-------------------------

/^l/{

# del current move and save others
  s///;x

# clear path
  /@ /{s// @/;bx;}
# push load
  /@o /{s// @o/;bx;}

# enter overdot
  /@\./{s// !/;bx;}
# continue overdot
  /!\./{s//.!/;bx;}
# out overdot
  /! /{s//.@/;bx;}

# enter load overdot
  /@o\./{s// @O/;bx;}
# enter overdot with load
  /@O\./{s// !O/;bx;}
# continue overdot with load
  /!O\./{s//.!O/;bx;}
# out load overdot / enter overdot
  /@O /{s// !o/;bx;}
# out load overdot / continue overdot
  /!O /{s//.!o/;bx;}
# out overdot with load
  /!o /{s//.@o/;bx;}
# out overdot with load / enter overdot
  /!o\./{s//.@O/;bx;}

# can't pass
  bx
}


# -------------[ DOWN ]--------------------------

/^j/{

# del current move and save others
  s///;x

# clear path
  /@\(.\{22\}\) /{s// \1@/;bx;}
# push load
  /@\(.\{22\}\)o\(.\{22\}\) /{s// \1@\2o/;bx;}

# enter overdot
  /@\(.\{22\}\)\./{s// \1!/;bx;}
# continue overdot
  /!\(.\{22\}\)\./{s//.\1!/;bx;}
# out overdot
  /!\(.\{22\}\) /{s//.\1@/;bx;}

# enter load overdot
  /@\(.\{22\}\)o\(.\{22\}\)\./{s// \1@\2O/;bx;}
# enter overdot with load
  /@\(.\{22\}\)O\(.\{22\}\)\./{s// \1!\2O/;bx;}
# continue overdot with load
  /!\(.\{22\}\)O\(.\{22\}\)\./{s//.\1!\2O/;bx;}
# out load overdot / enter overdot
  /@\(.\{22\}\)O\(.\{22\}\) /{s// \1!\2o/;bx;}
# out load overdot / continue overdot
  /!\(.\{22\}\)O\(.\{22\}\) /{s//.\1!\2o/;bx;}
# out overdot with load
  /!\(.\{22\}\)o\(.\{22\}\) /{s//.\1@\2o/;bx;}
# out overdot with load / enter overdot
  /!\(.\{22\}\)o\(.\{22\}\)\./{s//.\1@\2O/;bx;}

# target not free
  bx
}


# ---------------[ UP ]--------------------------

/^k/{

# del current move and save others
  s///;x

# clear path
  / \(.\{22\}\)@/{s//@\1 /;bx;}
# push load
  / \(.\{22\}\)o\(.\{22\}\)@/{s//o\1@\2 /;bx;}

# enter overdot
  /\.\(.\{22\}\)@/{s//!\1 /;bx;}
# continue overdot
  /\.\(.\{22\}\)!/{s//!\1./;bx;}
# out overdot
  / \(.\{22\}\)!/{s//@\1./;bx;}

# enter load overdot
  /\.\(.\{22\}\)o\(.\{22\}\)@/{s//O\1@\2 /;bx;}
# enter overdot with load
  /\.\(.\{22\}\)O\(.\{22\}\)@/{s//O\1!\2 /;bx;}
# continue overdot with load
  /\.\(.\{22\}\)O\(.\{22\}\)!/{s//O\1!\2./;bx;}
# out load overdot / enter overdot
  / \(.\{22\}\)O\(.\{22\}\)@/{s//o\1!\2 /;bx;}
# out load overdot / continue overdot
  / \(.\{22\}\)O\(.\{22\}\)!/{s//o\1!\2./;bx;}
# out overdot with load
  / \(.\{22\}\)o\(.\{22\}\)!/{s//o\1@\2./;bx;}
# out overdot with load / enter overdot
  /\.\(.\{22\}\)o\(.\{22\}\)!/{s//O\1@\2./;bx;}

# target not free
  bx
}

# wrong command, do nothing
x


# ----------------[ THE END ]-----------------
:x

# no more load ('o'), level finished!
/LEVEL \([1-9]\|[1-8][0-9]\)[^0-9]/{
/[ @!%.]o\|o[ @!%.]/!{
s/LEVEL \([0-9]*\).*$/&\n    (( SUCCESS! ))\
\
    Next level: \1/

    # replace all trailing 9s by _ (any other character except digits, could
    # be used)
    :inc_d
    s/9\(_*\)$/_\1/
    tinc_d

    # incr last digit only.  The first line adds a most-significant
    # digit of 1 if we have to add a digit.
    #
    # The tn commands are not necessary, but make the thing
    # faster

    s/\(l\)\(_*\)$/\11\2/; tinc_n
    s/8\(_*\)$/9\1/; tinc_n
    s/7\(_*\)$/8\1/; tinc_n
    s/6\(_*\)$/7\1/; tinc_n
    s/5\(_*\)$/6\1/; tinc_n
    s/4\(_*\)$/5\1/; tinc_n
    s/3\(_*\)$/4\1/; tinc_n
    s/2\(_*\)$/3\1/; tinc_n
    s/1\(_*\)$/2\1/; tinc_n
    s/0\(_*\)$/1\1/; tinc_n

    :inc_n
    y/_/0/

s/Next level: \([0-9]*\)$/<a href="\/game\/level\1\/">Go to level \1!<\/a>/

p
bgame_exit
}
}
/[ @!%.]o\|o[ @!%.]/!{s/$/     (( VICTORY! ))/p;bgame_exit;}

# save current position on hold space
x

# nice loop for accumulated moves
/./{bini;}

# Render current state
x
s/o\+/<code class="s_b">&<\/code>/g
s/\.\+/<code class="s_bp">&<\/code>/g
s/O\+/<code class="s_bpr">&<\/code>/g
s/%\+/<code class="s_w">&<\/code>/g
s/@\+/<code class="s_y">&<\/code>/g
s/!\+/<code class="s_yobp">&<\/code>/g
p


:game_exit
i\
</pre>\
</div>\
<div class="legend box last">\
<h2>Legend</h2>\
<ul>\
<li><code class="s_b">o</code> box</li>\
<li><code class="s_bp">.</code> box place</li>\
<li><code class="s_bpr">O</code> box placed right</li>\
<li><code class="s_w">%</code> wall</li>\
<li><code class="s_y">@</code> you</li>\
<li><code class="s_yobp">!</code> you over a box place</li>\
</ul>\
<hr>\
<p>A tribute to culture of\
PS-HTTPD, SED Sokoban,\
all good DSLs, and other awesome and inspiring projects.</p>\
<hr>\
<p>For more SED awesomeness look <a href="http://sed.sourceforge.net/grabbag/scripts/">here</a>.<\p>\
<hr>\
<p>Tip: standard Sokoban saves can be used now (hint: modify URL).</p>\
<hr>\
<p>If you are GitHub user, please consider starring <a href="https://github.com/xkollar/sed-httpd">the repo</a> O:-).</p>\
</div>\
</div>\
</body>\
</html>
q
